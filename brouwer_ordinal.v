Module Part_1 .

Inductive 自然数 : Type := ゼロ : 自然数 | 後者関数 : 自然数 -> 自然数 .

Inductive ブラウワー順序数 : Type
    :=
        ブラウワー順序数のゼロ : ブラウワー順序数
        |
        ブラウワー順序数の後者関数 : ブラウワー順序数 -> ブラウワー順序数
        |
        ブラウワー順序数の極限 : ( 自然数 -> ブラウワー順序数 ) -> ブラウワー順序数
.

End Part_1 .

Module Part_2 .

Inductive ゼロ段階のブラウワー順序数 : Type := .

Inductive 一段階のブラウワー順序数 : Type
    :=
        一段階のブラウワー順序数のゼロ段階の列
            : ( ゼロ段階のブラウワー順序数 -> 一段階のブラウワー順序数 ) -> 一段階のブラウワー順序数
.

Inductive 二段階のブラウワー順序数 : Type
    :=
        二段階のブラウワー順序数のゼロ段階の列
            : ( ゼロ段階のブラウワー順序数 -> 二段階のブラウワー順序数 ) -> 二段階のブラウワー順序数
        |
        二段階のブラウワー順序数の一段階の列
            : ( 一段階のブラウワー順序数 -> 二段階のブラウワー順序数 ) -> 二段階のブラウワー順序数
.

Inductive 三段階のブラウワー順序数 : Type
    :=
        三段階のブラウワー順序数のゼロ段階の列
            : ( ゼロ段階のブラウワー順序数 -> 三段階のブラウワー順序数 ) -> 三段階のブラウワー順序数
        |
        三段階のブラウワー順序数の一段階の列
            : ( 一段階のブラウワー順序数 -> 三段階のブラウワー順序数 ) -> 三段階のブラウワー順序数
        |
        三段階のブラウワー順序数の二段階の列
            : ( 二段階のブラウワー順序数 -> 三段階のブラウワー順序数 ) -> 三段階のブラウワー順序数
.

Inductive 四段階のブラウワー順序数 : Type
    :=
        四段階のブラウワー順序数のゼロ段階の列
            : ( ゼロ段階のブラウワー順序数 -> 四段階のブラウワー順序数 ) -> 四段階のブラウワー順序数
        |
        四段階のブラウワー順序数の一段階の列
            : ( 一段階のブラウワー順序数 -> 四段階のブラウワー順序数 ) -> 四段階のブラウワー順序数
        |
        四段階のブラウワー順序数の二段階の列
            : ( 二段階のブラウワー順序数 -> 四段階のブラウワー順序数 ) -> 四段階のブラウワー順序数
        |
        四段階のブラウワー順序数の三段階の列
            : ( 三段階のブラウワー順序数 -> 四段階のブラウワー順序数 ) -> 四段階のブラウワー順序数
.

Definition ゼロ段階のブラウワー順序数の場合分け
    : forall 目標 : Type , ゼロ段階のブラウワー順序数 -> 目標
    := fun 目標 : Type => fun 対象 : ゼロ段階のブラウワー順序数 => match 対象 with end
.

Definition 一段階のブラウワー順序数のゼロ : 一段階のブラウワー順序数
    :=
        一段階のブラウワー順序数のゼロ段階の列
            ( ゼロ段階のブラウワー順序数の場合分け 一段階のブラウワー順序数 )
.

Definition 一段階のブラウワー順序数の場合分け
    : forall 目標 : Type , 目標 -> 一段階のブラウワー順序数 -> 目標
    := fun 目標 : Type => fun 処理 : 目標 => fun 対象 : 一段階のブラウワー順序数 => 処理
.

Definition 二段階のブラウワー順序数のゼロ : 二段階のブラウワー順序数
    :=
        二段階のブラウワー順序数のゼロ段階の列
            ( ゼロ段階のブラウワー順序数の場合分け 二段階のブラウワー順序数 )
.

Definition 二段階のブラウワー順序数の後者関数 : 二段階のブラウワー順序数 -> 二段階のブラウワー順序数
    :=
        fun x : 二段階のブラウワー順序数 =>
        二段階のブラウワー順序数の一段階の列
            ( 一段階のブラウワー順序数の場合分け 二段階のブラウワー順序数 x )
.

Definition 二段階のブラウワー順序数の場合分け
    : forall 目標 : Type , 目標 -> ( 目標 -> 目標 ) -> 二段階のブラウワー順序数 -> 目標
    :=
        fun 目標 : Type =>
        fun ゼロの場合の処理 : 目標 =>
        fun 後者の場合の処理 : 目標 -> 目標 =>
        fix 再帰 ( 対象 : 二段階のブラウワー順序数 ) { struct 対象 } : 目標
            :=
                match
                    対象
                with
                    二段階のブラウワー順序数のゼロ段階の列 対象_ゼロ段階の列 => ゼロの場合の処理
                    |
                    二段階のブラウワー順序数の一段階の列 対象_一段階の列
                        => 再帰 ( 対象_一段階の列 一段階のブラウワー順序数のゼロ )
                end
.

Definition 三段階のブラウワー順序数のゼロ : 三段階のブラウワー順序数
    :=
        三段階のブラウワー順序数のゼロ段階の列
            ( ゼロ段階のブラウワー順序数の場合分け 三段階のブラウワー順序数 )
.

Definition 三段階のブラウワー順序数の後者関数 : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        三段階のブラウワー順序数の一段階の列
            ( 一段階のブラウワー順序数の場合分け 三段階のブラウワー順序数 x )
.

Definition 三段階のブラウワー順序数の繰り返し適用の強上限
    :
        三段階のブラウワー順序数
        ->
        ( 三段階のブラウワー順序数 -> 三段階のブラウワー順序数 )
        ->
        三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        fun f : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数 =>
        三段階のブラウワー順序数の二段階の列
            ( 二段階のブラウワー順序数の場合分け 三段階のブラウワー順序数 x f )
.

Definition 三段階のブラウワー順序数のオメガ : 三段階のブラウワー順序数
    :=
        三段階のブラウワー順序数の繰り返し適用の強上限
            三段階のブラウワー順序数のゼロ
            三段階のブラウワー順序数の後者関数
.

Definition 三段階のブラウワー順序数の加算
    : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        fix a ( y : 三段階のブラウワー順序数 ) { struct y } : 三段階のブラウワー順序数
            :=
                match
                    y
                with
                    三段階のブラウワー順序数のゼロ段階の列 y_ゼロ段階の列 => x
                    |
                    三段階のブラウワー順序数の一段階の列 y_一段階の列
                        =>
                            三段階のブラウワー順序数の一段階の列
                                ( fun i : 一段階のブラウワー順序数 => a ( y_一段階の列 i ) )
                    |
                    三段階のブラウワー順序数の二段階の列 y_二段階の列
                        =>
                            三段階のブラウワー順序数の二段階の列
                                ( fun i : 二段階のブラウワー順序数 => a ( y_二段階の列 i ) )
                end
.

Definition 三段階のブラウワー順序数の乗算
    : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        fix a ( y : 三段階のブラウワー順序数 ) { struct y } : 三段階のブラウワー順序数
            :=
                match
                    y
                with
                    三段階のブラウワー順序数のゼロ段階の列 y_ゼロ段階の列
                        => 三段階のブラウワー順序数のゼロ
                    |
                    三段階のブラウワー順序数の一段階の列 y_一段階の列
                        =>
                            三段階のブラウワー順序数の加算
                                x
                                ( a ( y_一段階の列 一段階のブラウワー順序数のゼロ ) )
                    |
                    三段階のブラウワー順序数の二段階の列 y_二段階の列
                        =>
                            三段階のブラウワー順序数の二段階の列
                                ( fun i : 二段階のブラウワー順序数 => a ( y_二段階の列 i ) )
                end
.

Definition 三段階のブラウワー順序数の冪乗
    : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        fix a ( y : 三段階のブラウワー順序数 ) { struct y } : 三段階のブラウワー順序数
            :=
                match
                    y
                with
                    三段階のブラウワー順序数のゼロ段階の列 y_ゼロ段階の列
                        => 三段階のブラウワー順序数の後者関数 三段階のブラウワー順序数のゼロ
                    |
                    三段階のブラウワー順序数の一段階の列 y_一段階の列
                        =>
                            三段階のブラウワー順序数の乗算
                                x
                                ( a ( y_一段階の列 一段階のブラウワー順序数のゼロ ) )
                    |
                    三段階のブラウワー順序数の二段階の列 y_二段階の列
                        =>
                            三段階のブラウワー順序数の二段階の列
                                ( fun i : 二段階のブラウワー順序数 => a ( y_二段階の列 i ) )
                end
.

Definition 三段階のブラウワー順序数のオメガ冪
    : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    := 三段階のブラウワー順序数の冪乗 三段階のブラウワー順序数のオメガ
.

Definition 三段階のブラウワー順序数のオメガ超冪
    : 三段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fun x : 三段階のブラウワー順序数 =>
        三段階のブラウワー順序数の繰り返し適用の強上限
            ( 三段階のブラウワー順序数の加算 x 三段階のブラウワー順序数のオメガ )
            三段階のブラウワー順序数のオメガ冪
.

Definition 三段階のブラウワー順序数のエプシロンゼロ : 三段階のブラウワー順序数
    :=
        三段階のブラウワー順序数の繰り返し適用の強上限
            三段階のブラウワー順序数のオメガ
            三段階のブラウワー順序数のオメガ冪
.

Definition 四段階のブラウワー順序数から三段階のブラウワー順序数への崩壊
    : 四段階のブラウワー順序数 -> 三段階のブラウワー順序数
    :=
        fix a ( x : 四段階のブラウワー順序数 ) { struct x } : 三段階のブラウワー順序数
            :=
                match
                    x
                with
                    四段階のブラウワー順序数のゼロ段階の列 x_ゼロ段階の列
                        => 三段階のブラウワー順序数のエプシロンゼロ
                    |
                    四段階のブラウワー順序数の一段階の列 x_一段階の列
                        =>
                            三段階のブラウワー順序数のオメガ超冪
                                ( a ( x_一段階の列 一段階のブラウワー順序数のゼロ ) )
                    |
                    四段階のブラウワー順序数の二段階の列 x_二段階の列
                        =>
                            三段階のブラウワー順序数の二段階の列
                                ( fun i : 二段階のブラウワー順序数 => a ( x_二段階の列 i ) )
                    |
                    四段階のブラウワー順序数の三段階の列 x_三段階の列
                        =>
                            三段階のブラウワー順序数の繰り返し適用の強上限
                                三段階のブラウワー順序数のオメガ
                                ( fun y : 三段階のブラウワー順序数 => a ( x_三段階の列 y ) )
                end
.

End Part_2 .
